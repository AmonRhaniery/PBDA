PREFIX ds: <http://dados.gov.br/dataset/cgeo_vw_per_pess65oumais#> 
PREFIX RDF: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ibge: <https://metadados.ibge.gov.br/#> 
PREFIX gn: <http://www.geonames.org/ontology#> 
PREFIX ogc: <http://www.opengis.net/rdf#> 
PREFIX j.0: <http://w3c/future-csv-vocab/> 
PREFIX j.1: <file:///Users/amonrhaniery/eclipse-workspace/hierarquiaurbana/input/vw_per_pess65oumais.csv#>
PREFIX sdmx-dimension: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX sdmx-measure: <http://purl.org/linked-data/sdmx/2009/measure#>
PREFIX sdmx-concept: <http://purl.org/linked-data/sdmx/2009/concept#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
    ?refCidade a qb:DimensionProperty;
        rdfs:subPropertyOf sdmx-dimension:refArea;
        qb:concept sdmx-concept:refArea;
        rdfs:range gn:P.

    ?dsPERPESS65oumais a qb:MeasureProperty;
        rdfs:subPropertyOf sdmx-measure:obsValue;
        rdfs:label "Percentual de pessoas de 65 anos ou mais de idade"@pt;
        rdfs:label "Percentage of the population 65 years of age or older"@en;
        rdfs:range xsd:decimal.

    ?dsPESS65oumais a qb:MeasureProperty;
        rdfs:subPropertyOf sdmx-measure:obsValue;
        rdfs:label "Pessoas residentes de 65 anos ou mais de idade"@pt;
        rdfs:label "Number of inhabitants 65 years of age or older"@en;
        rdfs:range xsd:decimal.

    ?componente qb:measure ?dsPERPESS65oumais ;
         qb:measure ?dsPESS65oumais ;
        qb:dimension sdmx-dimension:refPeriod ;
         qb:dimension ?refCidade .

    ?dsd a qb:DataStructureDefinition;
        qb:component ?componente.

    ?vw_per_pess65oumais a qb:DataSet;
        rdfs:label "Pessoas de 65 anos ou mais de idade"@pt;
        rdfs:label "People 65 years of age or older"@en;
        rdfs:comment "Percentual de pessoas de 65 anos ou mais de idade em relação à população residente total, na data de referência do CensoDemográfico.Fonte: IBGE, Censo Demográfico 2010 e Malha municipal 2010."@pt ;
        qb:structure ?dsd.

    ?row a qb:Observation ;
        ds:PESS65oumais ?PESS65oumais;
        qb:dataSet ?vw_per_pess65oumais;
        sdmx-dimension:refPeriod ?year ;
        ds:PERPESS65oumais ?PER_PESS65oumais;
        ds:refCidade ?city.
        
        ?city a ibge:Cidade;
            a ogc:Polygon;
            gn:name ?nome;
            gn:population ?POP_TOT;
            gn:parentADM1 ?UF;
            ogc:awSKT ?the_geom2;
            ibge:classeIdade65 ?classe .
        
} WHERE {
?row j.1:nome ?nome;
    j.1:UF ?UF ;
    j.1:POP_TOT ?POP_TOT ;
    j.1:PESS65oumais ?PESS65oumais ;
    j.1:PER_PESS65oumais ?PER_PESS65oumais ;
    j.1:classe ?classe ;
    j.1:geom ?the_geom .
    BIND(STRDT(STR(?the_geom), <http://www.opengis.net/rdf#WKTLiteral>) AS ?the_geom2)
    BIND(ENCODE_FOR_URI( STR( ?nome ) ) AS ?nome2)
    BIND(URI(CONCAT("https://cidades.ibge.gov.br/#", STR( ?nome2 )))  AS ?city)
    BIND(URI("http://dados.gov.br/dataset/cgeo_vw_per_pessoas_pess65oumais#dsd") AS ?dsd)
    BIND(URI("http://dados.gov.br/dataset/cgeo_vw_per_pessoas_pess65oumais#") AS ?vw_per_pess65oumais)
    BIND(URI("http://dados.gov.br/dataset/cgeo_vw_per_pessoas_pess65oumais#componente") AS ?componente)
    BIND(URI(CONCAT("http://dados.gov.br/dataset/cgeo_vw_per_pessoas_pess65oumais#", "classe"))  AS ?dsclasse)
    BIND(URI(CONCAT("http://dados.gov.br/dataset/cgeo_vw_per_pessoas_pess65oumais#", "PESS65oumais"))  AS ?dsPESS65oumais)
    BIND(URI(CONCAT("http://dados.gov.br/dataset/cgeo_vw_per_pessoas_pess65oumais#", "PERPESS65oumais"))  AS ?dsPERPESS65oumais)
    BIND(URI(CONCAT("http://dados.gov.br/dataset/cgeo_vw_per_pessoas_pess65oumais#", "refCidade"))  AS ?refCidade)
}