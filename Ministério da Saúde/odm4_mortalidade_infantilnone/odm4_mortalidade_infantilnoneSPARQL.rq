PREFIX ds: <http://dados.gov.br/dataset/odm4_mortalidade_infantil#> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ibge: <https://metadados.ibge.gov.br/#> 
PREFIX gn: <http://www.geonames.org/ontology#> 
PREFIX j.0: <http://w3c/future-csv-vocab/> 
PREFIX j.1: <file:///Users/amonrhaniery/eclipse-workspace/hierarquiaurbana/input/odm4_mortalidade_infantilnone.csv#>
PREFIX sdmx-dimension: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX sdmx-measure: <http://purl.org/linked-data/sdmx/2009/measure#>
PREFIX sdmx-concept: <http://purl.org/linked-data/sdmx/2009/concept#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>

CONSTRUCT {

    ?refCidade a qb:DimensionProperty;
        rdfs:subPropertyOf sdmx-dimension:refArea;
        qb:concept sdmx-concept:refArea;
        rdfs:range gn:P.

    ?dsObito a qb:MeasureProperty;
        rdfs:subPropertyOf sdmx-measure:obsValue;
        rdfs:label "Percentual de óbitos infantis investigados"@pt;
        rdfs:label "Percentage of infant deaths investigated"@en;
        qb:concept 	<http://vocabularies.unesco.org/thesaurus/concept5070> ;
        rdfs:range xsd:decimal.

    ?componente qb:measure ?dsObito; 
        qb:dimension sdmx-dimension:refPeriod ;
        qb:dimension ?refCidade.
    
    ?dsd a qb:DataStructureDefinition;
        qb:component ?componente.

    ?csv a dcat:Distribution;
        dcat:downloadURL ?downuri;
        dcat:mediaType "text/csv" .
        
    ?odm4_mortalidade_infantil a qb:DataSet;
        rdfs:label "Percentual de óbitos infantis investigados"@pt;
        rdfs:label "Percentage of infant deaths investigated"@en;
        rdfs:comment "Número de óbitos de menores de um ano de idade notificados ao SIM, com ficha síntese digitada no módulo de investigaçãodo SIM.Monitora as ações de vigilância do óbito infantil contribuindo para a redução da taxa de mortalidade infantil Periodicidade de Atualização: Anual Método de Cálculo: Valor Absoluto"@pt ;
        a dcat:Dataset;
        dcat:landingPage ?uri;
        dcat:distribution ?csv;
        qb:structure ?dsd.

    ?row a qb:Observation ;
        ds:obitos ?indicx;
        qb:dataSet ?odm4_mortalidade_infantil;
        sdmx-dimension:refPeriod "2018"^^xsd:integer;
        ds:refCidade ?city.
        
        ?city a ibge:Cidade;
            gn:name ?nome.
        
} WHERE {
?row j.1:no_cidade ?nome;
    j.1:indicx ?indicx .
    BIND(ENCODE_FOR_URI( STR( ?nome ) ) AS ?nome2)
    BIND(URI("http://dados.gov.br/dataset/odm4_mortalidade_infantil")AS ?uri)
    BIND(URI("http://i3geo.saude.gov.br/i3geo/ogc.php?service=WFS&version=1.0.0&request=GetFeature&typeName=odm4_mortalidade_infantil&outputFormat=CSV")AS ?downuri)
    BIND(URI("http://dados.gov.br/dataset/odm4_mortalidade_infantil")AS ?odm4_mortalidade_infantil)
    BIND(URI("http://dados.gov.br/dataset/odm4_mortalidade_infantil#csv")AS ?csv)
    BIND(URI("http://dados.gov.br/dataset/odm4_mortalidade_infantil#dsd")AS ?dsd)
    BIND(URI("http://dados.gov.br/dataset/odm4_mortalidade_infantil#componente")AS ?componente)
    BIND(URI(CONCAT("https://cidades.ibge.gov.br/#", STR( ?nome2 )))  AS ?city)
    BIND(URI(CONCAT("http://dados.gov.br/dataset/odm4_mortalidade_infantil#", "obitos"))  AS ?dsObito)
    BIND(URI(CONCAT("http://dados.gov.br/dataset/odm4_mortalidade_infantil#", "refCidade"))  AS ?refCidade)
}